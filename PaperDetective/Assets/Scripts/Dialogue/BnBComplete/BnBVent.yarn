title: BnBVent
---
//Check if the player grabbed the fragment.
<<declare $hasVentKeyFragment = false>>

<<if $hasVentKeyFragment == false>>
    Narrator: There seems to be a broken key fragment glinting within the vent.
    Narrator: If only you had something to reach into the vent…
    <<once>>
        <<RevealNote MenuManager BnBFragmentLong>>
    <<endonce>>
<<else>>
    <<if $musicOn == true>>
        Narrator: As you put your ear to the vent, a whisper seems to reach your ears.
        Narrator: However, it's impossible to make out the sound with the music playing.
    <<else>>
        Narrator: Listening to the vent once again, a whimpering sound can be heard somewhere in the darkness.
        Narrator: There also seems to be something else, almost like a low humming, distinct from the sound of machinery
    <<endif>>
<<endif>>
===

title: BnBVent_Screwdriver
---
<<if $hasVentKeyFragment == false>>
    Narrator: The screwdriver manages to pass through the vent, but it just barely can’t reach the item within.
    Narrator: If only you could reach a little farther with it…
    <<once>>
        <<RevealNote MenuManager BnBFragmentLonger>>
    <<endonce>>
<<else>>
    Narrator: You fail to notice anything else of interest in the vent.
<<endif>>
===

title: BnBVent_StringScrewdriver
---
<<once>>
    Narrator: You lower the screwdriver into the vent, and the key slides onto the magnetic screwdriver.
    Narrator: You manage to pull the key fragment out of the vent.
    <<AddItem Inventory VentKeyFragment>>
    <<set $hasVentKeyFragment = true>>
    <<stop>>
<<endonce>>

Narrator: You fail to notice anything else of interest in the vent.
===

title: BnBVent_Wrong
---
<<if $hasVentKeyFragment == false>>
    Narrator: You try to use this item to get through the vent, but it fails to reach the glistening object within.
    Narrator: If only you had a thinner object to reach in…
<<else>>
    Narrator: You fail to notice anything else of interest in the vent.
<<endif>>
===