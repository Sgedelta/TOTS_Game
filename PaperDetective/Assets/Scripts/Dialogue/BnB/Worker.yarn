title: WorkerManager
---
//Contain and record all the variables for the Worker NPC.

//firstTalk determines if this is the first time the player has ever interacted with this NPC.
<<declare $worker_firstTalk = false>>

//beforeDoor determines if the NPC has been interacted with but the first door is not opened.
<<declare $worker_beforeDoor = false>>

//afterDoor determines if this is the first interaction AFTER the door has been opened.
<<declare $worker_afterDoor = false>>

//For the Worker, check if they have given the player the key.
<<declare $worker_playerKnowsBasement = false>>
<<declare $worker_keyGiven = false>>
===

title: Worker
---
//Play this section the first time the player interacts with this NPC at all.
<<if $worker_firstTalk == false>>
    Worker: Hello, are you a guest here by any chance?
    You: Yes, I was told to ask you where the key to that loud room is.
    Worker: Ah, I’m terribly sorry, I can’t quite remember where I placed it.
    Worker: I never get to clean that room, as the hosts’ son is almost always in there.
    Worker: I left the key somewhere since I wasn’t able to use it, but I forgot where that was.
    You: Well, does anyone else know where it might be?
    Worker: You’re free to ask, but I doubt they would know anything. 
    <<set $worker_firstTalk = true>>
    <<stop>>

//Play AFTER the first interaction but BEFORE the first door is opened.
<<elseif $worker_beforeDoor == false>>
    Worker: If you could help me find the key, I’d greatly appreciate it!
    <<stop>>

//Play the first time AFTER the first door is opened.
<<elseif $worker_afterDoor == false>>
    Worker: Oh, this is quite odd.
    Worker: This room seems a bit cleaner than I thought it would be, I thought the man was far less tidy.
    You: Do you know where he is?
    Worker: No, I’m not too familiar with his habits, but he does normally turn everything off before leaving the house.
    Worker: Thank you for your help though, I’ll see what I can fix up in here before his return.
    <<set $worker_afterDoor = true>>
    <<stop>>

<<endif>>

//Play this if the player learns about the noise coming from the basement(from Resident 1).
<<if $worker_playerKnowsBasement == true && $worker_keyGiven == false>>
    Worker: Ah, I’m just about wrapped up here. Is there anything you need?
    You: Yes, someone said there was a noise coming from the basement. Do you have a way in there?
    Worker: Ah, yes, I actually have that key with me!
    Worker: I’ll just be finishing up here before I head down there. You can have the key if you’d like to check it out first.
    You: Thank you, I’ll be right back then.
    <<Add Inventory BasementKey>>
    <<set $worker_keyGiven = true>>
    <<stop>>

//Play this final chunk after all other dialogue has been exhausted.
<<else>>
    Worker: Just use that key to get into the basement, I’ll be right after you.
    <<stop>>

<<endif>>
===
